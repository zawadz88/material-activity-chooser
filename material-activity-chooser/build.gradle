apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'jacoco'

ext {
    bintrayRepo = 'maven'
    bintrayName = POM_ARTIFACT_ID

    publishedGroupId = POM_GROUP_ID
    libraryName = 'MaterialActivityChooser'
    artifact = POM_ARTIFACT_ID

    libraryDescription = 'This library allows to use Material activity choosers from Jelly Bean+.'

    siteUrl = 'https://github.com/zawadz88/material-activity-chooser'
    gitUrl = 'https://github.com/zawadz88/material-activity-chooser.git'

    libraryVersion = POM_VERSION

    developerId = 'zawadz88'
    developerName = 'Piotr Zawadzki'
    developerEmail = 'zawadz88@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    publishNonDefault true

    compileSdkVersion project.androidCompileSdkVersion
    buildToolsVersion ("$androidBuildToolsVersion")

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion project.androidMinSdkVersion
        targetSdkVersion project.androidTargetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    sourceSets.all { sourceSet ->
        sourceSet.res.srcDirs += ['src/main/res-public']
    }

    testOptions {
        //workaround so that code coverage is working with Robolectric: http://stackoverflow.com/a/35769943/973379
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {

    compile ("com.android.support:support-v4:$androidSupportLibraryVersion")
    compile ("com.android.support:appcompat-v7:$androidSupportLibraryVersion")
    compile ("com.android.support:support-annotations:$androidSupportLibraryVersion")
    compile ("com.android.support:recyclerview-v7:$androidSupportLibraryVersion")
    compile ("com.android.support:design:$androidSupportLibraryVersion")

    testCompile("junit:junit:$junitVersion")
    testCompile("org.mockito:mockito-core:$mockitoVersion")
    testCompile("org.robolectric:robolectric:$robolectricVersion") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

gradle.taskGraph.beforeTask { Task task ->
    if (task instanceof Javadoc) {
        def releaseVariant = android.libraryVariants.find { it.buildType.name == 'release' }
        Javadoc javadocTask = (Javadoc) task
        javadocTask.source = android.sourceSets.main.java.srcDirs
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        javadocTask.classpath = files(releaseVariant.javaCompile.classpath.files) + files(ext.androidJar)
    }
}

apply from: '../installv1.gradle'
apply from: '../bintrayv1.gradle'